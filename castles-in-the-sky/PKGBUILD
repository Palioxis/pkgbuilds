# Maintainer: Can Celasun <dcelasun[at]gmail[dot]com>
pkgname=castles-in-the-sky
pkgver=1
pkgrel=1
pkgdesc="A game by the Tall Trees"
arch=(i686 x86_64)
url="http://thetalltreesgames.co.uk/CastlesInTheSky/"
license=('custom')
depends=()
[ "$CARCH" = i686   ] && depends=(wine)
[ "$CARCH" = x86_64 ] && depends=(bin32-wine)
makedepends=(p7zip)
_sourcefile=castlesinthesky_win_12232013.exe
noextract=(${_sourcefile})
source=(castles-in-the-sky ${_sourcefile})
md5sums=('bac49864ef515a40aeb7f41bc9fb0890'
         '9b9efdc7a6c21bdc9137de05b59f582d')
package() {
  cd "${srcdir}"
  
  7z x -aoa -o"${srcdir}/exe" ${_sourcefile}
  
  install -d -m755  "${pkgdir}/usr/share/${pkgname}"
  install -d -m755  "${pkgdir}/usr/share/licenses/${pkgname}"
  
  cp -ra "${srcdir}"/exe/* "${pkgdir}/usr/share/${pkgname}"
  cp "${srcdir}/exe/License.txt" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  
  find "${pkgdir}/usr/share/${pkgname}" -type d -exec chmod 755 "{}" \;
  find "${pkgdir}/usr/share/${pkgname}" -type f -exec chmod 644 "{}" \;


  # Remove spaces in the exe file
  mv "${pkgdir}/usr/share/${pkgname}/Castles in the Sky.exe" "${pkgdir}/usr/share/${pkgname}/CastlesInTheSky.exe"
  
  install -d -m755 "${pkgdir}/usr/bin"
  install -m755 castles-in-the-sky "${pkgdir}/usr/bin"
  }
