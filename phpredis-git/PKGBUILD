# Maintainer: Can Celasun <dcelasun[at]gmail[dot]com>

pkgname=phpredis-git
pkgver=20120316
pkgrel=1
pkgdesc="A PHP extension for Redis"
arch=('i686' 'x86_64')
url="https://github.com/nicolasff/phpredis"
license=('PHP')
makedepends=('git')
depends=('php')
_ininame="redis.ini"
_inifile="etc/php/conf.d/$_ininame"
backup=("$_inifile")

_gitroot=https://github.com/nicolasff/phpredis.git
_gitname=phpredis

build() {
    cd "$srcdir"
    msg "Connecting to GIT server...."

    if [[ -d "$_gitname" ]]; then
        cd "$_gitname" && git pull origin
        msg "The local files are updated."
    else
        git clone "$_gitroot" "$_gitname"
    fi

    msg "GIT checkout done or server timeout"
    msg "Starting build..."

    cd $srcdir/phpredis

    phpize
    ./configure --prefix=/usr --enable-redis
    make
    #make test
}

package() {
    cd $srcdir/phpredis

    make INSTALL_ROOT="$pkgdir" install
    echo ";extension=redis.so" > "$_ininame"
    install -vDm644 "$_ininame" "$pkgdir/$_inifile"
}

